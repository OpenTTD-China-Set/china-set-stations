// these values shouldn't be changed...
#define TILE_X_NE 0
#define TILE_X_SW 1
#define TILE_X_NW 2
#define TILE_X_SE 3
#define TILE_X_N 4
#define TILE_X_S 5
#define TILE_X_W 6
#define TILE_X_E 7
#define TILE_SELF 8

#define TILE_Y_NW TILE_X_NE
#define TILE_Y_SE TILE_X_SW
#define TILE_Y_NE TILE_X_NW
#define TILE_Y_SW TILE_X_SE
#define TILE_Y_N TILE_X_N
#define TILE_Y_S TILE_X_S
#define TILE_Y_W TILE_X_E
#define TILE_Y_E TILE_X_W

// these are fine
#define TEMP_RAIL_CONTINUATION 9
#define TEMP_NEARBY_TILE_STATION 10
#define TEMP_NEARBY_TILE_PERPENDICULAR 11
#define TEMP_NEARBY_TILE_GRF 12

#define NORMAL_PLATFORM_SPRITESET(num, name)\
spriteset (s_platform_##num, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {\
    template_platform_NW("platforms/" + name)\
    template_platform_NE("platforms/" + name)\
    template_platform_SE("platforms/" + name)\
    template_platform_SW("platforms/" + name)\
    \
    template_platform_NW("asym_platforms/" + name + "_slope")\
    template_asymplat_NE("asym_platforms/" + name + "_slope")\
    template_asymplat_SE("asym_platforms/" + name + "_slope")\
    template_platform_SW("asym_platforms/" + name + "_slope")\
    \
    template_asymplat_NW("asym_platforms/" + name + "_slope")\
    template_platform_NE("asym_platforms/" + name + "_slope")\
    template_platform_SE("asym_platforms/" + name + "_slope")\
    template_asymplat_SW("asym_platforms/" + name + "_slope")\
}

#define SHELTER_SPRITESET(num, name)\
spriteset (s_shelter_##num, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {\
    NW: template_platform_zero(0, "shelters/" + name)\
    NE: template_platform_zero(1, "shelters/" + name)\
    SE: template_platform_zero(2, "shelters/" + name)\
    SW: template_platform_zero(3, "shelters/" + name)\
}

NORMAL_PLATFORM_SPRITESET(0, "platform_concrete")
NORMAL_PLATFORM_SPRITESET(1, "platform_brick")

SHELTER_SPRITESET(0, "shelter1")
SHELTER_SPRITESET(1, "shelter2")

spriteset (s_test_bufferstop, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    NW: template_bufferstop_NW("extras/bufferstop")
    NE: template_bufferstop_NE("extras/bufferstop")
    SE: template_bufferstop_SE("extras/bufferstop")
    SW: template_bufferstop_SW("extras/bufferstop")
}

/*
spriteset (s_test_fence, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    NW: template_fence_NW("fences/fence1")
    NE: template_fence_NE("fences/fence1")
    SE: template_fence_SE("fences/fence1")
    SW: template_fence_SW("fences/fence1")
}*/

#define GET_NPLAT_FORMATION \
    ((!getbits(LOAD_TEMP(TEMP_NEARBY_TILE_STATION), TILE_X_SW, 1) | getbits(LOAD_TEMP(TILE_X_SW), NPLAT_BIT, 1) & getbits(LOAD_TEMP(TEMP_NEARBY_TILE_GRF), TILE_X_SW, 1)) << 0|\
     (!getbits(LOAD_TEMP(TEMP_NEARBY_TILE_STATION), TILE_X_NE, 1) | getbits(LOAD_TEMP(TILE_X_NE), NPLAT_BIT, 1) & getbits(LOAD_TEMP(TEMP_NEARBY_TILE_GRF), TILE_X_NE, 1)) << 1)

#define GET_SPLAT_FORMATION \
    ((!getbits(LOAD_TEMP(TEMP_NEARBY_TILE_STATION), TILE_X_SW, 1) | getbits(LOAD_TEMP(TILE_X_SW), SPLAT_BIT, 1) & getbits(LOAD_TEMP(TEMP_NEARBY_TILE_GRF), TILE_X_SW, 1)) << 0|\
     (!getbits(LOAD_TEMP(TEMP_NEARBY_TILE_STATION), TILE_X_NE, 1) | getbits(LOAD_TEMP(TILE_X_NE), SPLAT_BIT, 1) & getbits(LOAD_TEMP(TEMP_NEARBY_TILE_GRF), TILE_X_NE, 1)) << 1)

spritelayout sp_test_x(hide_platform_nw, hide_platform_se, hide_shelter, ground_type) {
    ground {sprite: ground_type == 1? GROUNDSPRITE_NORMAL: ground_type == 2? GROUNDSPRITE_CONCRETE: GROUNDSPRITE_RAIL_X;}
    building { // bufferstop
        sprite: s_test_bufferstop(NE);
        BUFFERSTOP_BBOX_NE // macro
        hide_sprite: getbits(LOAD_TEMP(TEMP_RAIL_CONTINUATION), TILE_X_NE, 1) || ground_type;
    }
    building { // bufferstop
        sprite: s_test_bufferstop(SW);
        BUFFERSTOP_BBOX_SW // macro
        hide_sprite: getbits(LOAD_TEMP(TEMP_RAIL_CONTINUATION), TILE_X_SW, 1) || ground_type;
    }
    building { // station platform, macro is used to define the bounding box.
        sprite: CUSTOM(0, 0 + GET_NPLAT_FORMATION % 3 * 4);
        PLATFORM_BBOX_NW // macro
        hide_sprite: hide_platform_nw;
    }
    childsprite { // shelter
        sprite: CUSTOM(1, 0);
        hide_sprite: !!(GET_NPLAT_FORMATION % 3) || hide_shelter;
    }
    building {
        sprite: CUSTOM(0, 2 + GET_SPLAT_FORMATION % 3 * 4);
        PLATFORM_BBOX_SE // macro
        hide_sprite: hide_platform_se;
    }
    childsprite {
        sprite: CUSTOM(1, 2);
        hide_sprite: !!(GET_SPLAT_FORMATION % 3) || hide_shelter;
    }
}

spritelayout sp_test_y(hide_platform_ne, hide_platform_sw, hide_shelter, ground_type) {
    ground {sprite: ground_type == 1? GROUNDSPRITE_NORMAL: ground_type == 2? GROUNDSPRITE_CONCRETE: GROUNDSPRITE_RAIL_Y;}
    building {
        sprite: s_test_bufferstop(NW);
        BUFFERSTOP_BBOX_NW // macro
        hide_sprite: getbits(LOAD_TEMP(TEMP_RAIL_CONTINUATION), TILE_X_NE, 1) || ground_type;
    }
    building {
        sprite: s_test_bufferstop(SE);
        BUFFERSTOP_BBOX_SE // macro
        hide_sprite: getbits(LOAD_TEMP(TEMP_RAIL_CONTINUATION), TILE_X_SW, 1) || ground_type;
    }
    building { // station platform, macros are still used
        sprite: CUSTOM(0, 1 + GET_NPLAT_FORMATION % 3 * 4);
        PLATFORM_BBOX_NE // macro
        hide_sprite: hide_platform_ne;
    }
    childsprite {
        sprite: CUSTOM(1, 1);
        hide_sprite: !!(GET_NPLAT_FORMATION % 3) || hide_shelter;
    }
    building {
        sprite: CUSTOM(0, 3 + GET_SPLAT_FORMATION % 3 * 4);
        PLATFORM_BBOX_SW // macro
        hide_sprite: hide_platform_sw;
    }
    childsprite {
        sprite: CUSTOM(1, 3);
        hide_sprite: !!(GET_SPLAT_FORMATION % 3) || hide_shelter;
    }
}
/* about registers
 *
 */
switch (FEAT_STATIONS, SELF, sw_check_rail_continuation,
        hasbit(rail_continuation, 1) << TILE_X_NE|
        hasbit(rail_continuation, 0) << TILE_X_SW) {return;}


switch (FEAT_STATIONS, SELF, sw_check_nearby_tile_station,
        nearby_tile_is_station(-1, 0) << TILE_X_NE|
        nearby_tile_is_station( 1, 0) << TILE_X_SW|
        nearby_tile_is_station( 0,-1) << TILE_X_NW|
        nearby_tile_is_station( 0, 1) << TILE_X_SE|
        nearby_tile_is_station(-1,-1) << TILE_X_N|
        nearby_tile_is_station( 1, 1) << TILE_X_S|
        nearby_tile_is_station( 1,-1) << TILE_X_W|
        nearby_tile_is_station(-1, 1) << TILE_X_E) {return;}

switch (FEAT_STATIONS, SELF, sw_check_nearby_tile_perpendicular,
        nearby_tile_perpendicular(-1, 0) == 1 << TILE_X_NE|
        nearby_tile_perpendicular( 1, 0) == 1 << TILE_X_SW|
        nearby_tile_perpendicular( 0,-1) == 1 << TILE_X_NW|
        nearby_tile_perpendicular( 0, 1) == 1 << TILE_X_SE|
        nearby_tile_perpendicular(-1,-1) == 1 << TILE_X_N|
        nearby_tile_perpendicular( 1, 1) == 1 << TILE_X_S|
        nearby_tile_perpendicular( 1,-1) == 1 << TILE_X_W|
        nearby_tile_perpendicular(-1, 1) == 1 << TILE_X_E) {return;}

//TODO: if openttd/nml #330 is merged, change "other" to "same"

switch (FEAT_STATIONS, SELF, sw_check_nearby_tile_grf,
        nearby_tile_other_grf(-1, 0) << TILE_X_NE|
        nearby_tile_other_grf( 1, 0) << TILE_X_SW|
        nearby_tile_other_grf( 0,-1) << TILE_X_NW|
        nearby_tile_other_grf( 0, 1) << TILE_X_SE|
        nearby_tile_other_grf(-1,-1) << TILE_X_N|
        nearby_tile_other_grf( 1, 1) << TILE_X_S|
        nearby_tile_other_grf( 1,-1) << TILE_X_W|
        nearby_tile_other_grf(-1, 1) << TILE_X_E) {return;}

//switch (FEAT_STATIONS, SELF, sw_check_nearby_tile_grf,
//        nearby_tile_same_grf(-1, 0) << TILE_X_NE|
//        nearby_tile_same_grf( 1, 0) << TILE_X_SW|
//        nearby_tile_same_grf( 0,-1) << TILE_X_NW|
//        nearby_tile_same_grf( 0, 1) << TILE_X_SE|
//        nearby_tile_same_grf(-1,-1) << TILE_X_N|
//        nearby_tile_same_grf( 1, 1) << TILE_X_S|
//        nearby_tile_same_grf( 1,-1) << TILE_X_W|
//        nearby_tile_same_grf(-1, 1) << TILE_X_E) {return;}

switch (FEAT_STATIONS, SELF, sw_test, [
    STORE_TEMP(sw_check_rail_continuation(), TEMP_RAIL_CONTINUATION),
    STORE_TEMP(nearby_tile_station_id(-1, 0), TILE_X_NE),
    STORE_TEMP(nearby_tile_station_id( 1, 0), TILE_X_SW),
    STORE_TEMP(nearby_tile_station_id( 0,-1), TILE_X_NW),
    STORE_TEMP(nearby_tile_station_id( 0, 1), TILE_X_SE),
    STORE_TEMP(nearby_tile_station_id(-1,-1), TILE_X_N),
    STORE_TEMP(nearby_tile_station_id( 1, 1), TILE_X_S),
    STORE_TEMP(nearby_tile_station_id( 1,-1), TILE_X_W),
    STORE_TEMP(nearby_tile_station_id(-1, 1), TILE_X_E),
    STORE_TEMP(nearby_tile_station_id( 0, 0), TILE_SELF),
    STORE_TEMP(sw_check_nearby_tile_station(), TEMP_NEARBY_TILE_STATION),
    STORE_TEMP(sw_check_nearby_tile_perpendicular(), TEMP_NEARBY_TILE_PERPENDICULAR),
    STORE_TEMP(sw_check_nearby_tile_grf(), TEMP_NEARBY_TILE_GRF),
])
{return;}

#define STANDARD_STATION(hide_nplat, hide_splat, hide_shelter, platform_spriteset, shelter_spriteset) \
item (FEAT_STATIONS, i_station_##hide_nplat##hide_splat##hide_shelter##platform_spriteset##shelter_spriteset, \
                    hide_nplat << NPLAT_BIT | hide_splat << SPLAT_BIT | hide_shelter << SHELTER_BIT |\
                    platform_spriteset << PLATFORM_SPRITESET_BITS | shelter_spriteset << SHELTER_SPRITESET_BITS |\
                    STANDARD_PLATFORM_BITS) {\
    property {\
        class:      "CNS1";\
        classname:  string(STR_PLATFORMS);\
        name:       string(STR_STATION_WRAPPER,string(STR_NPLAT_##hide_nplat),string(STR_SPLAT_##hide_splat),string(STR_SHELTER_##hide_shelter));\
    }\
    graphics {\
        sprite_layouts: [sp_test_x(hide_nplat, hide_splat, hide_shelter, 0), sp_test_y(hide_nplat, hide_splat, hide_shelter, 0)];\
        custom_spritesets: [s_platform_##platform_spriteset, s_shelter_##shelter_spriteset];\
        prepare_layout: sw_test();\
    }\
}

#define TRACKLESS_STATION(hide_nplat, hide_splat, hide_shelter, platform_spriteset, shelter_spriteset, ground_type) \
item (FEAT_STATIONS, i_station_##hide_nplat##hide_splat##hide_shelter##platform_spriteset##shelter_spriteset##ground_type, \
                    hide_nplat << NPLAT_BIT | hide_splat << SPLAT_BIT | hide_shelter << SHELTER_BIT |\
                    platform_spriteset << PLATFORM_SPRITESET_BITS | shelter_spriteset << SHELTER_SPRITESET_BITS |\
                    STANDARD_PLATFORM_BITS | ground_type << GROUND_TYPE_BITS) {\
    property {\
        class:      "CNS2";\
        classname:  string(STR_TRACKLESS_PLATFORMS);\
        name:       string(STR_STATION_WRAPPER,string(STR_NPLAT_##hide_nplat),string(STR_SPLAT_##hide_splat),string(STR_SHELTER_##hide_shelter));\
        non_traversable_tiles: STAT_ALL_TILES;\
    }\
    graphics {\
        sprite_layouts: [sp_test_x(hide_nplat, hide_splat, hide_shelter, ground_type), sp_test_y(hide_nplat, hide_splat, hide_shelter, ground_type)];\
        custom_spritesets: [s_platform_##platform_spriteset, s_shelter_##shelter_spriteset];\
        prepare_layout: sw_test();\
    }\
}

#define GRASS 1
#define CEMENT 2

#define TRACKLESS_STATION_DEFINES(a,b,ground_type) \
TRACKLESS_STATION(0,0,0,a,b,ground_type)\
TRACKLESS_STATION(0,1,0,a,b,ground_type)\
TRACKLESS_STATION(1,0,0,a,b,ground_type)

#define STANDARD_STATION_DEFINES(a,b) \
STANDARD_STATION(0,0,0,a,b)\
STANDARD_STATION(0,1,0,a,b)\
STANDARD_STATION(1,0,0,a,b)

STANDARD_STATION_DEFINES(0,0)
STANDARD_STATION_DEFINES(0,1)
STANDARD_STATION_DEFINES(1,0)
STANDARD_STATION_DEFINES(1,1)
STANDARD_STATION(0,0,1,0,1)
STANDARD_STATION(0,1,1,0,1)
STANDARD_STATION(1,0,1,0,1)

STANDARD_STATION(1,1,1,0,0)

TRACKLESS_STATION_DEFINES(0,0,GRASS)
TRACKLESS_STATION_DEFINES(0,1,GRASS)
TRACKLESS_STATION_DEFINES(1,0,GRASS)
TRACKLESS_STATION_DEFINES(1,1,GRASS)
TRACKLESS_STATION_DEFINES(0,0,CEMENT)
TRACKLESS_STATION_DEFINES(0,1,CEMENT)
TRACKLESS_STATION_DEFINES(1,0,CEMENT)
TRACKLESS_STATION_DEFINES(1,1,CEMENT)

#undef STANDARD_STATION
#undef STANDARD_STATION_DEFINES

#undef TILE_SELF
#undef TILE_X_NE
#undef TILE_X_SW
#undef TILE_X_NW
#undef TILE_X_SE
#undef TILE_X_N
#undef TILE_X_S
#undef TILE_X_W
#undef TILE_X_E

#undef TILE_Y_NW
#undef TILE_Y_SE
#undef TILE_Y_NE
#undef TILE_Y_SW
#undef TILE_Y_N
#undef TILE_Y_S
#undef TILE_Y_W
#undef TILE_Y_E

#undef TEMP_RAIL_CONTINUATION
#undef TEMP_NEARBY_TILE_STATION
#undef TEMP_NEARBY_TILE_PERPENDICULAR

#undef GET_NPLAT_FORMATION
#undef GET_SPLAT_FORMATION

