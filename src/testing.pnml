spriteset (s_platform_0, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_platform_NW("platforms/platform_concrete")
    template_platform_NE("platforms/platform_concrete")
    template_platform_SE("platforms/platform_concrete")
    template_platform_SW("platforms/platform_concrete")
    // asym platforms, soooooo strange
    template_platform_NW("asym_platforms/platform_concrete_slope")
    template_asymplat_NE("asym_platforms/platform_concrete_slope")
    template_asymplat_SE("asym_platforms/platform_concrete_slope")
    template_platform_SW("asym_platforms/platform_concrete_slope")
    template_asymplat_NW("asym_platforms/platform_concrete_slope")
    template_platform_NE("asym_platforms/platform_concrete_slope")
    template_platform_SE("asym_platforms/platform_concrete_slope")
    template_asymplat_SW("asym_platforms/platform_concrete_slope")
}

spriteset (s_platform_1, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_platform_NW("platforms/platform_brick")
    template_platform_NE("platforms/platform_brick")
    template_platform_SE("platforms/platform_brick")
    template_platform_SW("platforms/platform_brick")
    // asym platforms, soooooo strange
    template_platform_NW("asym_platforms/platform_brick_slope")
    template_asymplat_NE("asym_platforms/platform_brick_slope")
    template_asymplat_SE("asym_platforms/platform_brick_slope")
    template_platform_SW("asym_platforms/platform_brick_slope")
    template_asymplat_NW("asym_platforms/platform_brick_slope")
    template_platform_NE("asym_platforms/platform_brick_slope")
    template_platform_SE("asym_platforms/platform_brick_slope")
    template_asymplat_SW("asym_platforms/platform_brick_slope")
}

spriteset (s_shelter_0, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    NW: template_platform_zero(0, "shelters/shelter1")
    NE: template_platform_zero(1, "shelters/shelter1")
    SE: template_platform_zero(2, "shelters/shelter1")
    SW: template_platform_zero(3, "shelters/shelter1")
}

spriteset (s_shelter_1, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    NW: template_platform_zero(0, "shelters/shelter2")
    NE: template_platform_zero(1, "shelters/shelter2")
    SE: template_platform_zero(2, "shelters/shelter2")
    SW: template_platform_zero(3, "shelters/shelter2")
}

spriteset (s_test_bufferstop, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    NW: template_bufferstop_NW("extras/bufferstop")
    NE: template_bufferstop_NE("extras/bufferstop")
    SE: template_bufferstop_SE("extras/bufferstop")
    SW: template_bufferstop_SW("extras/bufferstop")
}

spritelayout sp_test_x(hide_platform_nw, hide_platform_se, hide_shelter) {
    ground {sprite: GROUNDSPRITE_RAIL_X;}
    building {
        sprite: s_test_bufferstop(1);
        BUFFERSTOP_BBOX_NE // macro
        hide_sprite: getbits(LOAD_TEMP(0), 0, 1);
    }
    building {
        sprite: s_test_bufferstop(3);
        BUFFERSTOP_BBOX_SW // macro
        hide_sprite: getbits(LOAD_TEMP(0), 1, 1);
    }
    building { // station platform, macro is used to define the bounding box.
        sprite: CUSTOM(0, 0 + (getbits(LOAD_TEMP(3), NPLAT_BIT, 1) << 0 | getbits(LOAD_TEMP(2), NPLAT_BIT, 1) << 1) % 3 * 4);
        PLATFORM_BBOX_NW // macro
        hide_sprite: hide_platform_nw;
    }
    childsprite { // shelter
        sprite: CUSTOM(1, 0);
        hide_sprite: !!((getbits(LOAD_TEMP(3), NPLAT_BIT, 1) << 0 | getbits(LOAD_TEMP(2), NPLAT_BIT, 1) << 1) % 3) || hide_shelter;
    }
    building {
        sprite: CUSTOM(0, 2 + (getbits(LOAD_TEMP(3), SPLAT_BIT, 1) << 0 | getbits(LOAD_TEMP(2), SPLAT_BIT, 1) << 1) % 3 * 4);
        PLATFORM_BBOX_SE // macro
        hide_sprite: hide_platform_se;
    }
    childsprite {
        sprite: CUSTOM(1, 2);
        hide_sprite: !!((getbits(LOAD_TEMP(3), SPLAT_BIT, 1) << 0 | getbits(LOAD_TEMP(2), SPLAT_BIT, 1) << 1) % 3) || hide_shelter;
    }
}

spritelayout sp_test_y(hide_platform_ne, hide_platform_sw, hide_shelter) {
    ground {sprite: GROUNDSPRITE_RAIL_Y;}
    building {
        sprite: s_test_bufferstop(0);
        BUFFERSTOP_BBOX_NW // macro
        hide_sprite: getbits(LOAD_TEMP(0), 0, 1);
    }
    building {
        sprite: s_test_bufferstop(2);
        BUFFERSTOP_BBOX_SE // macro
        hide_sprite: getbits(LOAD_TEMP(0), 1, 1);
    }
    building { // station platform, macros are still used
        sprite: CUSTOM(0, 1 + (getbits(LOAD_TEMP(3), NPLAT_BIT, 1) << 0 | getbits(LOAD_TEMP(2), NPLAT_BIT, 1) << 1) % 3 * 4);
        PLATFORM_BBOX_NE // macro
        hide_sprite: hide_platform_ne;
    }
    childsprite {
        sprite: CUSTOM(1, 1);
        hide_sprite: !!((getbits(LOAD_TEMP(3), NPLAT_BIT, 1) << 0 | getbits(LOAD_TEMP(2), NPLAT_BIT, 1) << 1) % 3) || hide_shelter;
    }
    building {
        sprite: CUSTOM(0, 3 + (getbits(LOAD_TEMP(3), SPLAT_BIT, 1) << 0 | getbits(LOAD_TEMP(2), SPLAT_BIT, 1) << 1) % 3 * 4);
        PLATFORM_BBOX_SW // macro
        hide_sprite: hide_platform_sw;
    }
    childsprite {
        sprite: CUSTOM(1, 3);
        hide_sprite: !!((getbits(LOAD_TEMP(3), SPLAT_BIT, 1) << 0 | getbits(LOAD_TEMP(2), SPLAT_BIT, 1) << 1) % 3) || hide_shelter;
    }
}
/* about registers
 * 0: rail continuation, used for bufferstops
*/


switch (FEAT_STATIONS, SELF, sw_test, [
    STORE_TEMP(hasbit(rail_continuation, 1) << 0|
               hasbit(rail_continuation, 0) << 1, 0),
    STORE_TEMP(nearby_tile_is_station(-1,0) << 0|
               nearby_tile_is_station( 1,0) << 1, 1),
    STORE_TEMP(nearby_tile_station_id(-1,0), 2),
    STORE_TEMP(nearby_tile_station_id( 1,0), 3),
    STORE_TEMP(nearby_tile_station_id(0,-1), 4),
    STORE_TEMP(nearby_tile_station_id(0, 1), 5),
])
{return;}

#define STANDARD_STATION(hide_nplat, hide_splat, hide_shelter, platform_spriteset, shelter_spriteset, hide_fence) \
item (FEAT_STATIONS, i_station_##hide_nplat##hide_splat##hide_shelter##platform_spriteset##shelter_spriteset##hide_fence, \
                    hide_nplat << NPLAT_BIT | hide_splat << SPLAT_BIT | hide_shelter << SHELTER_BIT |\
                    platform_spriteset << PLATFORM_SPRITESET_BITS | shelter_spriteset << SHELTER_SPRITESET_BITS|\
                    hide_fence << FENCE_BIT | STANDARD_PLATFORM_BITS) {\
    property {\
        class:      "CNS1";\
        classname:  string(STR_TEST);\
        name:       string(STR_STATION_WRAPPER,string(STR_NPLAT_##hide_nplat),string(STR_SPLAT_##hide_splat),string(STR_SHELTER_##hide_shelter));\
    }\
    graphics {\
        sprite_layouts: [sp_test_x(hide_nplat, hide_splat, hide_shelter), sp_test_y(hide_nplat, hide_splat, hide_shelter)];\
        custom_spritesets: [s_platform_##platform_spriteset, s_shelter_##shelter_spriteset];\
        prepare_layout: sw_test();\
    }\
}


#define STANDARD_STATION_DEFINES(a,b) \
STANDARD_STATION(0,0,0,a,b,0)\
STANDARD_STATION(0,1,0,a,b,0)\
STANDARD_STATION(1,0,0,a,b,0)\
STANDARD_STATION(0,0,1,a,b,0)\
STANDARD_STATION(0,1,1,a,b,0)\
STANDARD_STATION(1,0,1,a,b,0)\

STANDARD_STATION_DEFINES(0,0)
STANDARD_STATION_DEFINES(0,1)
STANDARD_STATION_DEFINES(1,0)
STANDARD_STATION_DEFINES(1,1)

STANDARD_STATION(1,1,1,0,0,1)
#undef STANDARD_STATION
